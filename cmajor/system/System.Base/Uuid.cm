// =================================
// Copyright (c) 2025 Seppo Laakko
// Distributed under the MIT license
// =================================

using System.IO;

public delegate void PrintFn();

public PrintFn printFn;

public void SetPrintFn(PrintFn fn)
{
    printFn = fn;
}

public void Print()
{
    if (printFn != PrintFn())
    {
        printFn();
    }
}

namespace System
{
    public class Uuid
    {
        public const long size = 16;

        public typedef byte[size].Iterator Iterator;
        public typedef byte[size].ConstIterator ConstIterator;

        public inline Iterator Begin()
        {
            return data.Begin();
        }
        public inline ConstIterator Begin() const
        {
            return data.CBegin();
        }
        public inline ConstIterator CBegin() const
        {
            return data.CBegin();
        }
        public inline Iterator End()
        {
            return data.End();
        }
        public inline ConstIterator End() const
        {
            return data.CEnd();
        }
        public inline ConstIterator CEnd() const
        {
            return data.CEnd();
        }

        public Uuid() : data()
        {
        }
        public Uuid(ulong leftHalf, ulong rightHalf) : data()
        {
            ulong* l = cast<ulong*>(cast<void*>(Begin()));
            *l = leftHalf;
            ulong* r = cast<ulong*>(cast<void*>(Begin() + sizeof(ulong)));
            *r = rightHalf;
        }
        public ulong LeftHalf() const
        {
            ulong* l = cast<ulong*>(cast<void*>(Begin()));
            return *l;
        }
        public ulong RightHalf() const
        {
            ulong* r = cast<ulong*>(cast<void*>(Begin() + sizeof(ulong)));
            return *r;
        }
        public static Uuid Random()
        {
            return Uuid(Random64(), Random64());
        }
        public bool IsNil() const
        {
            for (byte x : data)
            {
                if (x != 0u) return false;
            }
            return true;
        }
        public byte[size] data;
    }

    public bool operator==(const Uuid& left, const Uuid& right)
    {
        void* l = cast<void*>(&left);
        ulong lx = cast<ulong>(l);
        if (lx <= 0x0000000000000100u)
        {
            Print();
        }
        void* r = cast<void*>(&right);
        ulong rx = cast<ulong>(r);
        if (rx <= 0x0000000000000100u)
        {
            Print();
        }
        #assert(lx > 0x0000000000000100u);
        #assert(rx > 0x0000000000000100u);
        for (long i = 0; i < Uuid.size; ++i)
        {
            if (left.data[i] != right.data[i]) return false;
        }
        return true;
    }

    public bool operator<(const Uuid& left, const Uuid& right)
    {
        if (left.LeftHalf() < right.LeftHalf()) return true;
        if (left.LeftHalf() > right.LeftHalf()) return false;
        return left.RightHalf() < right.RightHalf();
    }

    [nodiscard]
    public Result<string> ToString(const Uuid& uuid)
    {
        string s;
        int index = 0;
        for (byte x : uuid)
        {
            auto hexStringResult = ToHexString(x);
            if (hexStringResult.Error())
            {
                return Result<string>(ErrorId(hexStringResult.GetErrorId()));
            }
            auto toLowerResult = ToLower(hexStringResult.Value());
            if (toLowerResult.Error())
            {
                return Result<string>(ErrorId(toLowerResult.GetErrorId()));
            }
            s.Append(toLowerResult.Value());
            if (index == 3 || index == 5 || index == 7 || index == 9)
            {
                s.Append('-');
            }
            ++index;
        }
        return Result<string>(s);
    }

    [nodiscard]
    public Result<Uuid> ParseUuid(const string& uuidHexString)
    {
        if (uuidHexString.Length() != 2 * Uuid.size + 4)
        {
            string errorMessage = "wrong number of hex bytes in uuid string '" + uuidHexString + "'." + ToString(Uuid.size) + " hex bytes + 4 hyphens expected.";
            int errorId = RtmAllocateError(errorMessage.Chars());
            return Result<Uuid>(ErrorId(errorId));
        }
        Uuid uuid;
        int index = 0;
        for (long i = 0; i < Uuid.size; ++i)
        {
            string hexByteStr = uuidHexString.Substring(index, 2);
            auto hexByte = ParseHexByte(hexByteStr);
            if (hexByte.Error())
            {
                return Result<Uuid>(ErrorId(hexByte.GetErrorId()));
            }
            uuid.data[i] = hexByte.Value();
            ++index;
            ++index;
            if (i == 3 || i == 5 || i == 7 || i == 9)
            {
                ++index;
            }
        }
        return Result<Uuid>(uuid);
    }

    public TextWriter& operator<<(TextWriter& writer, const Uuid& uuid)
    {
        auto result = ToString(uuid);
        if (result.Error())
        {
            writer.SetErrorId(result.GetErrorId());
            return writer;
        }
        return writer << result.Value();
    }

}
