// =================================
// Copyright (c) 2024 Seppo Laakko
// Distributed under the MIT license
// =================================

using System;
using System.Windows.API;

namespace System.Windows
{
    public enum StandardCursorId : int
    {
        IDC_APPSTARTING = 32650,
        IDC_ARROW = 32512,
        IDC_CROSS = 32515,
        IDC_HAND = 32649,
        IDC_HELP = 32651,
        IDC_IBEAM = 32513,
        IDC_NO = 32648,
        IDC_SIZEALL = 32646,
        IDC_SIZENESW = 32643,
        IDC_SIZENS = 32645,
        IDC_SIZENWSE = 32642,
        IDC_SIZEWE = 32644,
        IDC_UPARROW = 32516,
        IDC_WAIT = 32514
    }

    public class Cursor : Resource
    {
        public Cursor() : cursorHandle(null), standard(false)
        {
        }
        public Cursor(void* cursorHandle_) : cursorHandle(cursorHandle_), standard(false)
        {
        }
        public Cursor(void* cursorHandle_, bool standard_) : cursorHandle(cursorHandle_), standard(standard_)
        {
        }
        public Cursor(Cursor&& that) : cursorHandle(that.cursorHandle), standard(that.standard)
        {
            that.cursorHandle = null;
            that.standard = false;
        }
        public ~Cursor()
        {
            if (cursorHandle != null && !standard)
            {
                WinDestroyCursor(cursorHandle);
            }
        }
        public default void operator=(Cursor&&);
        suppress Cursor(const Cursor&);
        suppress void operator=(const Cursor&);
        public void* Handle() const
        {
            return cursorHandle;
        }
        public bool IsStandard() const
        {
            return standard;
        }
        public void SetStandard()
        {
            standard = true;
        }
        private void* cursorHandle;
        private bool standard;
    }

    [nodiscard]
    public Result<Cursor> LoadStandardCursor(StandardCursorId cursorId)
    {
        void* cursorHandle = WinLoadStandardCursor(cast<int>(cursorId));
        if (cursorHandle == null)
        {
            int errorId = WinAllocateWindowsError("could not load standard cursor", WinGetLastError());
            return Result<Cursor>(ErrorId(errorId));
        }
        return Result<Cursor>(Cursor(cursorHandle, true));
    }

    [nodiscard]
    public Result<Cursor> LoadCursor(const string& cursorName)
    {
        auto cursorResult = LoadCursor(cursorName.Chars());
        if (cursorResult.Error())
        {
            return Result<Cursor>(ErrorId(cursorResult.GetErrorId()));
        }
        return Result<Cursor>(Cursor(cursorResult.Value()));
    }

    public Cursor GetCursor()
    {
        return Cursor(WinGetCursor(), true);
    }

    public void SetCursor(Cursor* cursor)
    {
        WinSetCursor(cursor->Handle());
    }
}
