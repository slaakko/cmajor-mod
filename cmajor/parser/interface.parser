// =================================
// Copyright (c) 2024 Seppo Laakko
// Distributed under the MIT license
// =================================

export module cmajor.interface.parser;

[interface]import cmajor.parser.context;
[interface]import cmajor.ast;
[interface]import soul.ast.span;
[implementation]import cmajor.token;
[implementation]import soul.lexer;
[implementation]import cmajor.lexer;
[implementation]import soul.ast.source.pos;
[implementation]import cmajor.attribute.parser;
[implementation]import cmajor.specifier.parser;
[implementation]import cmajor.identifier.parser;
[implementation]import cmajor.type.expr.parser;
[implementation]import cmajor.parameter.parser;

parser InterfaceParser
{
    lexer cmajor::lexer::CmajorLexer<char32_t>;

    using AttributeParser.Attributes;
    using SpecifierParser.Specifiers;
    using IdentifierParser.Identifier;
    using TypeExprParser.TypeExpr;
    using ParameterParser.ParameterList;

    Interface(cmajor::parser::context::Context* context, var soul::ast::Span s, var soul::ast::Span specifierSpan, 
        var std::unique_ptr<cmajor::ast::InterfaceNode> interfaceNode) : cmajor::ast::InterfaceNode*
        ::= 
        (
            Attributes(context):attrs?
            Specifiers:specifiers{ s = lexer.GetSpan(pos); specifierSpan = s; }
            INTERFACE 
            Identifier(context):id
            {
                interfaceNode.reset(new cmajor::ast::InterfaceNode(s, specifiers, id, attrs));
                interfaceNode->SetSpecifierSpan(specifierSpan);
            }
            LBRACE{ interfaceNode->SetBeginBraceSpan(lexer.GetSpan(pos)); }
            InterfaceContent(context, interfaceNode.get()):interfaceContent
            RBRACE{ interfaceNode->SetEndBraceSpan(lexer.GetSpan(pos)); }
        )
        {
            return interfaceNode.release();
        }
        ;

    InterfaceContent(cmajor::parser::context::Context* context, cmajor::ast::InterfaceNode* interfaceNode)
        ::= 
        (
            InterfaceMemberFunction(context):interfaceMemberFunction{ interfaceNode->AddMember(interfaceMemberFunction); }
        )*
        ;

    InterfaceMemberFunction(cmajor::parser::context::Context* context, var soul::ast::Span s, var soul::ast::Span groupIdSpan,
        var std::unique_ptr<cmajor::ast::MemberFunctionNode> memberFunctionNode) : cmajor::ast::MemberFunctionNode*
        ::=
        (
            Attributes(context):attrs?
            TypeExpr(context):returnType{ s = lexer.GetSpan(pos); }
            (InterfaceFunctionGroupId(context):groupId{ groupIdSpan = lexer.GetSpan(pos); })
            {

                memberFunctionNode.reset(new cmajor::ast::MemberFunctionNode(s, cmajor::ast::Specifiers(), returnType, groupId, attrs));
                memberFunctionNode->SetGroupIdSpan(groupIdSpan);
            }
            ParameterList(context, memberFunctionNode.get()):paramList 
            SEMICOLON
        )
        {
            return memberFunctionNode.release();
        }
        ;

    InterfaceFunctionGroupId(cmajor::parser::context::Context* context) : std::u32string
        ::= ID{ return lexer.GetToken(pos).ToString(); }
        ;
}
